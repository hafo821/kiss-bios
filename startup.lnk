OUTPUT_FORMAT(binary)
OUTPUT_ARCH(m68k)
ENTRY(_start)
SECTIONS
{
    /* things that live in ROM */
    .text SEGMENT_START("text", 0xFFF00000) : 
        { 
            __text_start = . ;
            *(.text .stub .text.* .gnu.linkonce.t.*) 
            _etext = . ; 
            *(.rodata .rodata.* .gnu.linkonce.r.*) 
            *(.rodata1) 
            __text_end = . ;
            __data_rom = . ; /* has to be last -- startup.s uses this to locate the .romdata section in ROM */
        }

    /* things that load to ROM, but later copied to SRAM */
    .romdata SEGMENT_START("data", 0xFFFE0000) : 
        AT ( ADDR (.text) + SIZEOF (.text) )
        { 
            __data_start = . ; 
            *(.data); 
            __data_end = . ; 
        }

    /* no init, lives in SRAM */
    .bss ADDR (.romdata) + SIZEOF(.romdata) :
        { 
            __bss_start = . ;
            *(.bss) *(COMMON) ;
            __bss_end = . ; 
        }
}

